use System.Collections

# TODO: Update recursive functions to be iterative
# TODO: Store width of fast lane skips to enable fast indexed access
# TODO: Implement SkipSet and SkipMultiSet and then implement SkipList and
#       SkipMultiList on top of them.

namespace SkipLists

    class Node<of T>
        where T must be IComparable

        var _height as int
        get height from var

        var _item as T
        get item from var

        # TODO: Change to Array
        var _nexts as List<of Node<of T>?>

        cue init(height as int, item as T)
            base.init
            _height = height
            _item = item
            _nexts = List<of Node<of T>>()
            for n in height
                _nexts.add(nil)

        def toList as List<of T>
        xs = [.item]
            next = _nexts[0]
            if next
                xs = xs + next.toList
            return xs

        def count as int
            n = 1
            next = _nexts[0]
            if next
                n = n + next.count
            return n

        def itemAt(index as int) as T
            require
                index > 0
                # Warning the next requirement is relatively slow on
                # big lists... so disable contracts in production
                index < .count
            body
                # TODO: Convert to faster skipList approach
                if index == 0
                    return .item
                return _nexts[0].itemAt(index - 1)

        def indexOf(item as T) as int
            require
                .contains(item)
            body
                if item == _item
                    return 0
                return 1 + _nexts[0].indexOf(item)

        def find(item as T) as int?
            body
                if item == _item
                    return 0
                next = _nexts[0]
                if next
                    return 1 + next.find(item)
                return nil

        def tempAttach(nextNode as Node<of T>)
            _nexts[0] = nextNode

        def insert(item as T) as Node<of T>
            ensure
                ((result == this) and (.count == old.count + 1)) or _
                    ((result <> this) and (.count == old.count))
                # item in result # TODO
            body
                newNode = Node<of T>(.height, item)
                if item.compareTo(.item) <= 0
                    newNode.tempAttach(this)
                    return newNode
                if _nexts[0]
                    _nexts[0] = _nexts[0].insert(item)
                else
                    _nexts[0] = newNode
                return this

        def contains(item as T) as bool
            if item == .item
                return true
            if _nexts[0]
                return _nexts[0].contains(item)
            return false


    class SkipList<of T>
        where T must be IComparable

        var _defaultMaxHeight as int = 16
            is shared
        get defaultMaxHeight from var is shared

        var _head as Node<of T>?
        pro head from var

        var _maxHeight as int
        pro maxHeight from var

        cue init(maxHeight as int, items as List<of T>)
            require
                maxHeight > 1
                maxHeight < 33  # assumes 32 bit random level generator
            body
                base.init
                .maxHeight = maxHeight
                .insertMany(items)

        cue init(items as List<of T>)
            .init(.defaultMaxHeight, items)

        cue init
            .init(List<of T>())

        def count as int
            test
                xs = SkipList<of String>()
                assert xs.count == 0
                xs.insert("a")
                assert xs.count == 1
                assert SkipList<of int>([1,2,3]).count == 3
            body
                if _head
                    return _head.count
                return 0

        def itemAt(index as int) as T
            require
                index >= 0
                index < .count
            test
                assert SkipList<of int>([1, 2, 3]).itemAt(0) == 1
                assert SkipList<of int>([1, 2, 3]).itemAt(1) == 2
                assert SkipList<of int>([1, 2, 3]).itemAt(2) == 3
            body
                return _head.itemAt(index)

        def insert(item as T) as SkipList<of T>
            ensure
                .count == old.count + 1
#                item in this # TODO: How do we implement this? e.g. __contains__
                result == this
            test
                assert SkipList<of int>([1, 2, 3])_
                           .insert(4).toList == [1, 2, 3, 4]
            body
                if _head
                    _head = _head.insert(item)
                else
                    _head = Node<of T>(.defaultMaxHeight, item)
                return this

        def insertMany(items as List<of T>) as SkipList<of T>
            ensure
                .count == old.count + items.count
                result == this
            test
                assert SkipList<of int>([2, 3]).insertMany([1, 4])_
                    .toList == [1, 2, 3, 4]
            body
                for item in items
                    .insert(item)
                return this

        def removeAt(index as int) as SkipList<of T>
            require
                index >= 0
                old.count > 0
            ensure
                .count == old.count - 1
                result == this
            test
                assert SkipList<of int>([1, 2, 3]).removeAt(0).toList == [2, 3]
                assert SkipList<of int>([1, 2, 3]).removeAt(1).toList == [1, 3]
                assert SkipList<of int>([1, 2, 3]).removeAt(2).toList == [1, 2]
            body
                # TODO: This
                return this

        def removeManyAt(index as int, n as int) as SkipList<of T>
            require
                index + n < old.count
            ensure
                .count == old.count - n
                result == this
            test
                assert SkipList<of int>([1, 2, 3]).removeManyAt(0, 2)_
                           .toList == [3]
                assert SkipList<of int>([1, 2, 3]).removeManyAt(1, 2)_
                           .toList == [1]
            body
                # for x_ in n
                #     _tempList.removeAt(index)
                return this

        def indexOf(item as T) as int
            require
                .contains(item)
            body
                return _head.indexOf(item)

        def find(item as T) as int?
            return _head.find(item)

        def contains(item as T) as bool
            if _head
                return _head.contains(item)
            return false

        def toList as List<of T>
            return _head.toList


    class BogusMain
        def main
            node = Node<of int>(16, 1)
            print node.count

            skipList = SkipList<of int>([1, 2, 3])
            print "Hi: [skipList.toList]"
            print skipList.maxHeight

            skipList = SkipList<of int>(4, [1, 2, 3])
            print "Hi: [skipList.toList]"
            print skipList.maxHeight

            skipList = SkipList<of int>(4, [1, 3, 2, 6, 5, 7, 7, 1, 9])
            print "Hi: [skipList.toList]"
            print skipList.maxHeight
            print skipList.indexOf(6)
